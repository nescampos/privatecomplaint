pragma language_version >= 0.16 && <= 0.18;

import CompactStandardLibrary;

// The type of institution reported
enum InstitutionType {
  PUBLIC,
  PRIVATE
}

// The Counter of the reports
export ledger reports: Counter;
// The Map of the reports
export ledger reportList: Map<Uint<64>,WhistleblowerNote>;

// The struct of the report
struct WhistleblowerNote {
    // Public and unique ID to track the complaint in the system (via the Counter)
    report_id: Uint<64>; 
    // Institution involved (can be a hash or a short string)
    institution_name: Opaque<"string">; 
    // Report details (the data with all the details about the corruption case)
    report_details: Opaque<"string">; 
    institution_type: InstitutionType;
    // Timestamp of the presentation
    timestamp: Uint<64>;
    // The public key of the informer (anonymous using the secret key and counter)
    informer: Bytes<32>;
}

// Constructor
constructor() {

}

// Function to generate the public key of the informer (anonymous using the secret key and counter)
circuit publicKey(reports: Field, sk: Bytes<32>): Bytes<32> {
  return persistentHash<Vector<3, Bytes<32>>>(
           [pad(32, "midnight:compliants:lock:pk"), reports as Bytes<32>, sk]);
}

// The secret key of the informer (anonymous)
witness secretKey(): Bytes<32>;

// Function to submit a report
export circuit submitReport(institution_name: Opaque<"string">, details: Opaque<"string">, institution_type: InstitutionType, current_timestamp: Uint<64>): Boolean {
    
    // Get the current report ID to assign
    const current_report: Uint<64> = reports.read();

    // Get the secret key of the informer
    const sk_informer = secretKey();

    // Create the report with all the details
    const newReport: WhistleblowerNote = WhistleblowerNote {
        report_id: current_report,
        institution_name: disclose(institution_name),
        report_details: disclose(details),
        timestamp: disclose(current_timestamp),
        institution_type: disclose(institution_type),
        informer: disclose(publicKey(current_report,sk_informer))
    };
    reportList.insert(current_report, newReport);
    
    reports.increment(1);

    return true;
}

// Function to get a report by ID to see all the information
export circuit getReport(report_id: Uint<64>): WhistleblowerNote {
    const report : WhistleblowerNote = reportList.lookup(disclose(report_id));
    return report;
}

// Function to verify if the informer is the one who submitted the report in case the justice wants to verify (selective privacy)
export circuit validateInformer(report_id: Uint<64>): Boolean {
    const sk = secretKey();
    const pk = publicKey(report_id, sk);
    const report : WhistleblowerNote = reportList.lookup(disclose(report_id));
    assert(report.informer == disclose(pk), "Invalid informer identity");
    return true;
}
